import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'me.tatarka.retrolambda'

android {
//    def isOnline = true
//    def forAppium = false

    compileSdkVersion 25
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "com.yusion.shanghai.yusion4s"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode rootProject.VersionCode
        versionName "${rootProject.VersionName}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi"
        }

        multiDexEnabled true

        buildConfigField("boolean", "forAppium", forAppium.toString())

        buildConfigField("boolean", "isOnline", isOnline.toString())
        def pgyer_appid = "2234913a0c85f5dc0e60faefe41c96e0"//红米手机图标不变
        def app_icon = "@mipmap/test_ic_launcher"
//        def app_icon = "@mipmap/ic_launcher"
        if (isOnline) {
            pgyer_appid = "a455421518edcbc4d3784f2b855b0691"
            app_icon = "@mipmap/ic_launcher"
        }
        manifestPlaceholders = ["PGYER_APPID": pgyer_appid, APP_ICON: app_icon]
    }
//    productFlavors{
//        online{
//
//        }
//
//        alpha{
//            applicationId "com.yusion.shanghai.yusion4s.alpha"
//        }
//    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        debug {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            String environment = "alpha"
            if (isOnline) environment = "product"
            //projectDir是app目录
            String prefix = ""
            if (forAppium) prefix = prefix + "appium_"
            output.outputFile = new File(projectDir.getAbsolutePath() + "/keystore/apks", prefix + environment + "-yusion4s-" + defaultConfig.versionName + "-" + buildTime() + ".apk")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets.main {
        assets.srcDirs = ['assets']
        jniLibs.srcDirs = ['libs']//指定jniLibs文件夹路径
        jni.srcDirs = []//不编译jni
    }
}

static def buildTime() {
    def df = new SimpleDateFormat("MMdd")
    df.setTimeZone(TimeZone.getDefault())
    return df.format(new Date())
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile(name: 'AliyunSdk-RC', ext: 'aar')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile project(path: ':PickerLib')
    compile files('libs/jcore-android-1.1.6.jar')
    compile files('libs/jpush-android-3.0.8.jar')
    //    compile 'com.pgyersdk:sdk:2.7.0'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.github.bumptech.glide:glide:4.0.0'
    compile 'com.github.bumptech.glide:compiler:4.0.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:4.0.0'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.github.hackware1993:MagicIndicator:1.5.0'
    compile 'com.chanven.lib:cptr:1.1.0'
    compile 'com.github.ybq:Android-SpinKit:1.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:26.0.0-alpha1'
    compile 'io.sentry:sentry-android:1.5.2'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.instabug.library:instabug:4.2.10'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.amap.api:location:latest.integration'
    compile 'com.shizhefei:LargeImageView:1.0.9'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'
    compile project(path: ':photoLibrary')
}
repositories {
    mavenCentral()
}

public class BuildTimeListener implements TaskExecutionListener, BuildListener {
    private Clock clock
    private times = []

    @Override
    void beforeExecute(Task task) {
        clock = new org.gradle.util.Clock()
    }

    @Override
    void afterExecute(Task task, TaskState taskState) {
        def ms = clock.timeInMs
        times.add([ms, task.path])

        //task.project.logger.warn "${task.path} spend ${ms}ms"
    }

    @Override
    void buildStarted(Gradle gradle) {

    }

    @Override
    void settingsEvaluated(Settings settings) {

    }

    @Override
    void projectsLoaded(Gradle gradle) {

    }

    @Override
    void projectsEvaluated(Gradle gradle) {

    }

    @Override
    void buildFinished(BuildResult result) {
        println "Task spend time:"
        for (time in times) {
            if (time[0] >= 50) {
                printf "%7sms  %s\n", time
            }
        }
    }
}

project.gradle.addListener(new BuildTimeListener())

afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += '--set-max-idx-number=48000'
        dx.additionalParameters += "--main-dex-list=$projectDir/multidex.keep".toString()
    }
}